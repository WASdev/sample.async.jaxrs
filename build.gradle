apply plugin: 'maven'
apply plugin: 'war'
apply plugin: 'liberty'

group = 'liberty.samples'
version = '1'

description = "WAS Liberty Sample - JAX-RS 2.0 Asynchronous processing Sample"

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.0'
    }
}

repositories {
    mavenCentral()   
}

dependencies {
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.apache.cxf', name: 'cxf-rt-rs-client', version:'3.1.1'
    testCompile group: 'org.glassfish', name: 'javax.json', version:'1.0.4'
    providedCompile group: 'javax', name: 'javaee-api', version:'7.0'
    libertyRuntime group: 'com.ibm.websphere.appserver.runtime', name: 'wlp-javaee7', version: '17.0.0.2'
}

ext {
    testServerHttpPort = 9081
    testServerHttpsPort = 9443 
    warContext = 'async-jaxrs'
    packagingType = 'usr'
}


war {
    archiveName = baseName + '.' + extension
}
    
liberty {
    server {
        configFile = file('src/main/liberty/config/server.xml')
        bootstrapProperties = ['default.http.port': testServerHttpPort, 
                               'default.https.port': testServerHttpsPort,
                               'appContext': warContext,
                               'appLocation': war.archiveName]
        apps = [war]
    }
    
    packageLiberty {
        include = packagingType
    }
}

test {
    println 'inside the unit test block'
    reports.html.destination = file("$buildDir/reports/unit")
    reports.junitXml.destination = file("$buildDir/test-results/unit")
    exclude '**/it/**'
}


task integrationTest(type: Test) {
    group 'Verification'
    description 'Runs the integration tests.'
    reports.html.destination = file("$buildDir/reports/it")
    reports.junitXml.destination = file("$buildDir/test-results/it")
    include '**/it/**'
    exclude '**/unit/**'

    systemProperties = ['liberty.test.port': testServerHttpPort, 'war.context': warContext]
}

task printMessageAboutRunningServer {
    doLast {
        println "The server is now running at http://localhost:${testServerHttpPort}/jaxrs-async"
        println "To stop the server run 'gradle libertyStop'"
    }
}

check.dependsOn 'integrationTest'
integrationTest.dependsOn 'libertyStart'
integrationTest.finalizedBy 'libertyStop'
libertyStart.finalizedBy 'printMessageAboutRunningServer'
